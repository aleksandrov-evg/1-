ВЫБРАТЬ
	ЗакупкиОбороты.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	ЗакупкиОбороты.АналитикаУчетаНоменклатуры.Серия.ВЭД_Импортер КАК Импортер,
	ВЫБОР
		КОГДА ЗакупкиОбороты.Регистратор ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
			ТОГДА "Таможенные пошлины"
		ИНАЧЕ "Стоимость закупки"
	КОНЕЦ КАК ВизЗатраты,
	ЗакупкиОбороты.Регистратор КАК Регистратор,
	ЗакупкиОбороты.Регистратор.Контрагент КАК Контрагент,
	ЗакупкиОбороты.АналитикаУчетаНоменклатуры.Серия КАК Серия,
	ЗакупкиОбороты.Организация КАК Организация,
	СУММА(ЗакупкиОбороты.КоличествоОборот) КАК КоличествоПриход,
	СУММА(ЗакупкиОбороты.СуммаБезНДСОборот) КАК СтоимостьПриход
ПОМЕСТИТЬ ДанныеЗакупки
{ВЫБРАТЬ
	Номенклатура.*,
	Импортер.*,
	ВизЗатраты,
	Регистратор.*,
	Контрагент.*,
	Серия.*,
	Организация.*,
	КоличествоПриход,
	СтоимостьПриход}
ИЗ
	РегистрНакопления.Закупки.Обороты(&НачалоПериода, &КонецПериода, Регистратор, ) КАК ЗакупкиОбороты
ГДЕ
	(ЗакупкиОбороты.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
			ИЛИ ЗакупкиОбороты.Регистратор ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
			ИЛИ ЗакупкиОбороты.Регистратор ССЫЛКА Документ.КорректировкаПриобретения)
{ГДЕ
	ЗакупкиОбороты.АналитикаУчетаНоменклатуры.Серия.*}

СГРУППИРОВАТЬ ПО
	ЗакупкиОбороты.Регистратор,
	ЗакупкиОбороты.АналитикаУчетаНоменклатуры.Номенклатура,
	ЗакупкиОбороты.Организация,
	ЗакупкиОбороты.АналитикаУчетаНоменклатуры.Серия.ВЭД_Импортер,
	ЗакупкиОбороты.Регистратор.Контрагент,
	ЗакупкиОбороты.АналитикаУчетаНоменклатуры.Серия

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ЗакупкиОбороты.АналитикаУчетаНоменклатуры.Номенклатура,
	ЗакупкиОбороты.АналитикаУчетаНоменклатуры.Серия.ВЭД_Импортер,
	"НДС",
	ЗакупкиОбороты.Регистратор,
	ЗакупкиОбороты.Регистратор.Контрагент,
	ЗакупкиОбороты.АналитикаУчетаНоменклатуры.Серия,
	ЗакупкиОбороты.Организация,
	СУММА(0),
	СУММА(ЗакупкиОбороты.СуммаОборот - ЗакупкиОбороты.СуммаБезНДСОборот)
ИЗ
	РегистрНакопления.Закупки.Обороты(&НачалоПериода, &КонецПериода, Регистратор, ) КАК ЗакупкиОбороты
ГДЕ
	(ЗакупкиОбороты.Регистратор ССЫЛКА Документ.ПриобретениеТоваровУслуг
			ИЛИ ЗакупкиОбороты.Регистратор ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
			ИЛИ ЗакупкиОбороты.Регистратор ССЫЛКА Документ.КорректировкаПриобретения
			ИЛИ ЗакупкиОбороты.Регистратор ССЫЛКА Документ.ЗаявлениеОВвозеТоваров)
	И ЗакупкиОбороты.СуммаОборот <> ЗакупкиОбороты.СуммаБезНДСОборот
{ГДЕ
	ЗакупкиОбороты.АналитикаУчетаНоменклатуры.Серия.*}

СГРУППИРОВАТЬ ПО
	ЗакупкиОбороты.Регистратор,
	ЗакупкиОбороты.АналитикаУчетаНоменклатуры.Номенклатура,
	ЗакупкиОбороты.Организация,
	ЗакупкиОбороты.АналитикаУчетаНоменклатуры.Серия.ВЭД_Импортер,
	ЗакупкиОбороты.Регистратор.Контрагент,
	ЗакупкиОбороты.АналитикаУчетаНоменклатуры.Серия
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ДанныеЗакупки.Регистратор КАК Регистратор,
	СУММА(ДанныеЗакупки.КоличествоПриход) КАК КоличествоПриход
ПОМЕСТИТЬ ЗакупкаПоДокументам
{ВЫБРАТЬ
	Регистратор.*,
	КоличествоПриход}
ИЗ
	ДанныеЗакупки КАК ДанныеЗакупки

СГРУППИРОВАТЬ ПО
	ДанныеЗакупки.Регистратор
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ДвиженияСерийТоваровОбороты.Серия КАК Серия,
	ДвиженияСерийТоваровОбороты.Номенклатура КАК Номенклатура,
	ДвиженияСерийТоваровОбороты.Документ КАК Документ,
	ДвиженияСерийТоваровОбороты.КоличествоОборот КАК КоличествоОборот
ПОМЕСТИТЬ ПеремещенияТоваров
{ВЫБРАТЬ
	Серия.*,
	Номенклатура.*,
	Документ.*,
	КоличествоОборот}
ИЗ
	РегистрНакопления.ДвиженияСерийТоваров.Обороты(&НачалоПериода, &КонецПериода, , ) КАК ДвиженияСерийТоваровОбороты
ГДЕ
	ДвиженияСерийТоваровОбороты.Документ ССЫЛКА Документ.ПеремещениеТоваров
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ПеремещенияТоваров.Документ КАК Документ,
	СУММА(ПеремещенияТоваров.КоличествоОборот) КАК КоличествоОборот
ПОМЕСТИТЬ ПеремещениеПоДокументам
{ВЫБРАТЬ
	Документ.*,
	КоличествоОборот}
ИЗ
	ПеремещенияТоваров КАК ПеремещенияТоваров

СГРУППИРОВАТЬ ПО
	ПеремещенияТоваров.Документ
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ПеремещенияТоваров.Серия КАК Серия,
	ПеремещенияТоваров.Номенклатура КАК Номенклатура,
	ПеремещенияТоваров.Документ КАК Документ,
	СУММА(ПеремещенияТоваров.КоличествоОборот / ПеремещениеПоДокументам.КоличествоОборот * ПартииПрочихРасходовОбороты.СтоимостьПриход) КАК Стоимость,
	ПартииПрочихРасходовОбороты.Регистратор КАК Регистратор,
	ПартииПрочихРасходовОбороты.СтатьяРасходов КАК СтатьяРасходов
ПОМЕСТИТЬ ЗатратыПоПеремещению
{ВЫБРАТЬ
	Серия.*,
	Номенклатура.*,
	Документ.*,
	Стоимость,
	Регистратор.*,
	СтатьяРасходов.*}
ИЗ
	ПеремещенияТоваров КАК ПеремещенияТоваров
		ЛЕВОЕ СОЕДИНЕНИЕ ПеремещениеПоДокументам КАК ПеремещениеПоДокументам
		ПО (ПеремещениеПоДокументам.Документ = ПеремещенияТоваров.Документ)
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииПрочихРасходов.Обороты(&НачалоПериода, &КонецПериода, Регистратор, ) КАК ПартииПрочихРасходовОбороты
		ПО (ПартииПрочихРасходовОбороты.АналитикаРасходов = ПеремещенияТоваров.Документ)

СГРУППИРОВАТЬ ПО
	ПеремещенияТоваров.Серия,
	ПеремещенияТоваров.Номенклатура,
	ПеремещенияТоваров.Документ,
	ПартииПрочихРасходовОбороты.Регистратор,
	ПартииПрочихРасходовОбороты.СтатьяРасходов
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ДанныеЗакупки.Номенклатура КАК Номенклатура,
	ДанныеЗакупки.Импортер КАК Импортер,
	ПартииПрочихРасходовОбороты.СтатьяРасходов КАК Расход,
	ПартииПрочихРасходовОбороты.Регистратор КАК Регистратор,
	ДанныеЗакупки.Контрагент КАК Контрагент,
	ДанныеЗакупки.Серия КАК Серия,
	ДанныеЗакупки.Организация КАК Организация,
	0 КАК Количество,
	ПартииПрочихРасходовОбороты.СтоимостьПриход * ДанныеЗакупки.КоличествоПриход / ЗакупкаПоДокументам.КоличествоПриход КАК СтоимостьПриход,
	ДанныеЗакупки.Регистратор КАК ДокументЗакупки
ПОМЕСТИТЬ ДанныеЗакупкиИтог
{ВЫБРАТЬ
	Номенклатура.*,
	Импортер.*,
	Расход.*,
	Регистратор.*,
	Контрагент.*,
	Серия.*,
	Организация.*,
	Количество,
	СтоимостьПриход,
	ДокументЗакупки.*}
ИЗ
	ДанныеЗакупки КАК ДанныеЗакупки
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииПрочихРасходов.Обороты(&НачалоПериода, &КонецПериода, Регистратор, ) КАК ПартииПрочихРасходовОбороты
		ПО ДанныеЗакупки.Регистратор = ПартииПрочихРасходовОбороты.АналитикаРасходов
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗакупкаПоДокументам КАК ЗакупкаПоДокументам
		ПО ДанныеЗакупки.Регистратор = ЗакупкаПоДокументам.Регистратор

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ДанныеЗакупки.Номенклатура,
	ДанныеЗакупки.Импортер,
	ЗатратыПоПеремещению.СтатьяРасходов,
	ЗатратыПоПеремещению.Регистратор,
	ДанныеЗакупки.Контрагент,
	ДанныеЗакупки.Серия,
	ДанныеЗакупки.Организация,
	0,
	ЗатратыПоПеремещению.Стоимость,
	ДанныеЗакупки.Регистратор
ИЗ
	ДанныеЗакупки КАК ДанныеЗакупки
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗатратыПоПеремещению КАК ЗатратыПоПеремещению
		ПО ДанныеЗакупки.Номенклатура = ЗатратыПоПеремещению.Номенклатура
			И ДанныеЗакупки.Серия.Номер = ЗатратыПоПеремещению.Серия.Номер

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ДанныеЗакупки.Номенклатура,
	ДанныеЗакупки.Импортер,
	ДанныеЗакупки.ВизЗатраты,
	ДанныеЗакупки.Регистратор,
	ДанныеЗакупки.Контрагент,
	ДанныеЗакупки.Серия,
	ДанныеЗакупки.Организация,
	ДанныеЗакупки.КоличествоПриход,
	ДанныеЗакупки.СтоимостьПриход,
	ДанныеЗакупки.Регистратор
ИЗ
	ДанныеЗакупки КАК ДанныеЗакупки
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ВЫБОР
		КОГДА Продажи.Регистратор = НЕОПРЕДЕЛЕНО
			ТОГДА 0
		ИНАЧЕ Продажи.Регистратор
	КОНЕЦ КАК Регистратор,
	Продажи.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	Продажи.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	Продажи.АналитикаУчетаНоменклатуры.Серия КАК Серия,
	Продажи.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	Продажи.КоличествоОборот КАК Количество,
	Продажи.СуммаВыручкиОборот КАК Выручка
ПОМЕСТИТЬ ВТ_Продажи
ИЗ
	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(&НачалоПериода {(&НачалоПериода)}, &КонецПериода {(&ОкончаниеПериода)}, Авто, ) КАК Продажи
		{ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимости
		ПО (НАЧАЛОПЕРИОДА(Продажи.ПериодМесяц, МЕСЯЦ) = НАЧАЛОПЕРИОДА(РасчетСебестоимости.Ссылка.Дата, МЕСЯЦ))
			И Продажи.АналитикаУчетаПоПартнерам.Организация = РасчетСебестоимости.Организация
			И (РасчетСебестоимости.Ссылка.Проведен)
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтоимостьТоваров КАК СтоимостьНоменклатуры
		ПО Продажи.АналитикаУчетаНоменклатуры = СтоимостьНоменклатуры.АналитикаУчетаНоменклатуры
			И Продажи.АналитикаУчетаПоПартнерам.Организация = СтоимостьНоменклатуры.Организация
			И Продажи.ВидЗапасов = СтоимостьНоменклатуры.ВидЗапасов
			И (СтоимостьНоменклатуры.Период = НАЧАЛОПЕРИОДА(Продажи.ПериодМесяц, МЕСЯЦ))
			И Продажи.РазделУчета = СтоимостьНоменклатуры.РазделУчета
			И (ЕСТЬNULL(РасчетСебестоимости.Ссылка.ПредварительныйРасчет, ЛОЖЬ))}
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	Товары.Ссылка КАК Ссылка,
	Товары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	МАКСИМУМ(Товары.Упаковка) КАК Упаковка
ПОМЕСТИТЬ ВТ_Реализации
ИЗ
	Документ.РеализацияТоваровУслуг.Товары КАК Товары
ГДЕ
	(Товары.Ссылка, Товары.АналитикаУчетаНоменклатуры) В
			(ВЫБРАТЬ
				ВТ_Продажи.Регистратор КАК Регистратор,
				ВТ_Продажи.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
			ИЗ
				ВТ_Продажи КАК ВТ_Продажи)
	И Товары.Упаковка.Наименование ПОДОБНО "шт%"

СГРУППИРОВАТЬ ПО
	Товары.Ссылка,
	Товары.АналитикаУчетаНоменклатуры

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	КорректировкаРеализацииРасхождения.Ссылка,
	КорректировкаРеализацииРасхождения.АналитикаУчетаНоменклатуры,
	МАКСИМУМ(КорректировкаРеализацииРасхождения.Упаковка)
ИЗ
	Документ.КорректировкаРеализации.Расхождения КАК КорректировкаРеализацииРасхождения
ГДЕ
	(КорректировкаРеализацииРасхождения.Ссылка, КорректировкаРеализацииРасхождения.АналитикаУчетаНоменклатуры) В
			(ВЫБРАТЬ
				ВТ_Продажи.Регистратор КАК Регистратор,
				ВТ_Продажи.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
			ИЗ
				ВТ_Продажи КАК ВТ_Продажи)
	И КорректировкаРеализацииРасхождения.Упаковка.Наименование ПОДОБНО "шт%"

СГРУППИРОВАТЬ ПО
	КорректировкаРеализацииРасхождения.Ссылка,
	КорректировкаРеализацииРасхождения.АналитикаУчетаНоменклатуры

ИНДЕКСИРОВАТЬ ПО
	Ссылка,
	АналитикаУчетаНоменклатуры
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ РАЗРЕШЕННЫЕ
	СУММА(Таблица.Количество) КАК Количество,
	Таблица.Номенклатура КАК Номенклатура,
	Таблица.Характеристика КАК Характеристика,
	Таблица.Серия КАК Серия,
	СУММА(Таблица.Выручка) КАК Выручка,
	МАКСИМУМ(Таблица.АналитикаУчетаНоменклатуры) КАК АналитикаУчетаНоменклатуры,
	Таблица.Регистратор КАК Регистратор,
	Таблица.АналитикаУчетаНоменклатуры.Партнер КАК Партнер
ПОМЕСТИТЬ ПродажиКг
ИЗ
	(ВЫБРАТЬ
		Продажи.Регистратор КАК Регистратор,
		Продажи.Номенклатура КАК Номенклатура,
		Продажи.Характеристика КАК Характеристика,
		Продажи.Серия КАК Серия,
		Продажи.Количество * ВЫБОР
			КОГДА ЕСТЬNULL(ВТ_Реализации.Упаковка.Знаменатель, 0) = 0
				ТОГДА 1
			ИНАЧЕ ВТ_Реализации.Упаковка.Числитель / ВТ_Реализации.Упаковка.Знаменатель
		КОНЕЦ КАК Количество,
		Продажи.Выручка КАК Выручка,
		Продажи.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	ИЗ
		ВТ_Продажи КАК Продажи
			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Реализации КАК ВТ_Реализации
			ПО Продажи.Регистратор = ВТ_Реализации.Ссылка
				И Продажи.АналитикаУчетаНоменклатуры = ВТ_Реализации.АналитикаУчетаНоменклатуры) КАК Таблица

СГРУППИРОВАТЬ ПО
	Таблица.Номенклатура,
	Таблица.Характеристика,
	Таблица.Серия,
	Таблица.Регистратор,
	Таблица.АналитикаУчетаНоменклатуры.Партнер
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ПродажиКг.Номенклатура КАК Номенклатура,
	ПродажиКг.Серия КАК Серия,
	ПродажиКг.Количество КАК КоличествоПродажа,
	ПродажиКг.Выручка КАК СуммаВыручки,
	ПродажиКг.Партнер КАК Партнер,
	ПродажиКг.Регистратор КАК Регистратор
ПОМЕСТИТЬ ДанныеПродажи
{ВЫБРАТЬ
	Номенклатура.*,
	Серия.*,
	КоличествоПродажа,
	СуммаВыручки,
	Партнер.*,
	Регистратор.*}
ИЗ
	ПродажиКг КАК ПродажиКг
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ДанныеПродажи.Регистратор КАК Регистратор,
	СУММА(ДанныеПродажи.КоличествоПродажа) КАК КоличествоПродажа
ПОМЕСТИТЬ КоличествоРеализации
{ВЫБРАТЬ
	КоличествоПродажа}
ИЗ
	ДанныеПродажи КАК ДанныеПродажи

СГРУППИРОВАТЬ ПО
	ДанныеПродажи.Регистратор
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ДанныеПродажи.Номенклатура КАК Номенклатура,
	ДанныеПродажи.Серия КАК Серия,
	0 КАК КоличествоПродажа,
	0 КАК СуммаВыручки,
	ДанныеПродажи.Партнер КАК Партнер,
	ПрочиеРасходыОбороты.Регистратор КАК Регистратор,
	ПрочиеРасходыОбороты.СуммаПриход * ДанныеПродажи.КоличествоПродажа / КоличествоРеализации.КоличествоПродажа КАК СуммаРасходы,
	ПрочиеРасходыОбороты.СтатьяРасходов КАК СтатьяРасходов
ПОМЕСТИТЬ ВыручкаИШтрафы
{ВЫБРАТЬ
	Номенклатура.*,
	Серия.*,
	КоличествоПродажа,
	СуммаВыручки,
	Партнер.*,
	Регистратор.*,
	СуммаРасходы,
	СтатьяРасходов.*}
ИЗ
	ДанныеПродажи КАК ДанныеПродажи
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КоличествоРеализации КАК КоличествоРеализации
		ПО ДанныеПродажи.Регистратор = КоличествоРеализации.Регистратор
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы.Обороты(, , Регистратор, ) КАК ПрочиеРасходыОбороты
		ПО ДанныеПродажи.Регистратор = ПрочиеРасходыОбороты.АналитикаРасходов
{ГДЕ
	ДанныеПродажи.Номенклатура.*,
	ДанныеПродажи.Серия.*}
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	ТоварыНаСкладахОстатки.Серия КАК Серия,
	ТоварыНаСкладахОстатки.ВНаличииОстаток КАК ОстатокНаСкладе
ПОМЕСТИТЬ ОстаткиНаСкладе
{ВЫБРАТЬ
	Номенклатура.*,
	Серия.*,
	ОстатокНаСкладе}
ИЗ
	РегистрНакопления.ТоварыНаСкладах.Остатки(
			&ТекущаяДата {(&ТекущаяДата)},
			Помещение.ВЭД_ВидПомещения = &ВидПомещения
				И Склад В (&Склад)) КАК ТоварыНаСкладахОстатки
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ПроизводствоБезЗаказаВыходныеИзделия.Номенклатура КАК Номенклатура,
	ПроизводствоБезЗаказаВыходныеИзделия.Серия КАК Серия,
	ПроизводствоБезЗаказаТрудозатраты.Количество КАК Количество,
	НАЧАЛОПЕРИОДА(ПроизводствоБезЗаказаТрудозатраты.Ссылка.Дата, ДЕНЬ) КАК Дата,
	ПроизводствоБезЗаказаВыходныеИзделия.Ссылка КАК Ссылка
ПОМЕСТИТЬ Трудозатраты
{ВЫБРАТЬ
	Номенклатура.*,
	Серия.*,
	Количество,
	Дата,
	Ссылка.*}
ИЗ
	Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ПроизводствоБезЗаказаВыходныеИзделия
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа.Трудозатраты КАК ПроизводствоБезЗаказаТрудозатраты
		ПО ПроизводствоБезЗаказаВыходныеИзделия.Ссылка = ПроизводствоБезЗаказаТрудозатраты.Ссылка
			И ПроизводствоБезЗаказаВыходныеИзделия.НомерГруппыЗатрат = ПроизводствоБезЗаказаТрудозатраты.НомерГруппыЗатрат
ГДЕ
	ПроизводствоБезЗаказаВыходныеИзделия.Ссылка.Проведен
	И ПроизводствоБезЗаказаВыходныеИзделия.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	Трудозатраты.Дата КАК Дата,
	СУММА(Трудозатраты.Количество) КАК Количество
ПОМЕСТИТЬ ТрудозатратыПоДате
{ВЫБРАТЬ
	Дата,
	Количество}
ИЗ
	Трудозатраты КАК Трудозатраты

СГРУППИРОВАТЬ ПО
	Трудозатраты.Дата
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	Трудозатраты.Номенклатура КАК Номенклатура,
	Трудозатраты.Серия КАК Серия,
	Трудозатраты.Количество / ТрудозатратыПоДате.Количество * &ФотЗаДень КАК СуммаРасхода,
	"ФОТ" КАК СтатьяРасходов,
	Трудозатраты.Ссылка КАК Ссылка
ПОМЕСТИТЬ РасчетФОТ
{ВЫБРАТЬ
	Номенклатура.*,
	Серия.*,
	СуммаРасхода,
	СтатьяРасходов,
	Ссылка.*}
ИЗ
	Трудозатраты КАК Трудозатраты
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТрудозатратыПоДате КАК ТрудозатратыПоДате
		ПО Трудозатраты.Дата = ТрудозатратыПоДате.Дата
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	Стоимость.Регистратор КАК Регистратор,
	Стоимость.Номенклатура КАК Номенклатура,
	Стоимость.КоличествоПриход КАК Количество,
	Стоимость.СтоимостьПриход КАК СуммаРасходы
ПОМЕСТИТЬ СтоимостьМатериалов
ИЗ
	РегистрНакопления.ВЭД_СтоимостьМатериаловКСписанию.Обороты(&НачалоПериода, &КонецПериода, Регистратор, ) КАК Стоимость
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ДанныеЗакупкиИтог.Серия КАК Серия,
	ДанныеЗакупкиИтог.Номенклатура КАК Номенклатура,
	ДанныеЗакупкиИтог.Регистратор КАК Регистратор,
	ДанныеЗакупкиИтог.Расход КАК Расход,
	0 КАК КоличествоПродажа,
	0 КАК СуммаВыручки,
	0 КАК Партнер,
	ДанныеЗакупкиИтог.СтоимостьПриход КАК СуммаРасходы,
	ДанныеЗакупкиИтог.Количество КАК Количество,
	0 КАК ОстатокНаСкладе,
	0 КАК КоличествоОтход,
	0 КАК КоличествоНаСтолах,
	0 КАК Цена
{ВЫБРАТЬ
	Серия.*,
	Номенклатура.*,
	Регистратор.*,
	Расход.*,
	КоличествоПродажа,
	СуммаВыручки,
	Партнер.*,
	СуммаРасходы,
	Количество,
	ОстатокНаСкладе,
	КоличествоОтход,
	КоличествоНаСтолах,
	Цена}
ИЗ
	ДанныеЗакупкиИтог КАК ДанныеЗакупкиИтог
{ГДЕ
	ДанныеЗакупкиИтог.Серия.*,
	ДанныеЗакупкиИтог.Номенклатура.*,
	ДанныеЗакупкиИтог.Расход.*}

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ВыручкаИШтрафы.Серия,
	ВыручкаИШтрафы.Номенклатура,
	ВыручкаИШтрафы.Регистратор,
	ВыручкаИШтрафы.СтатьяРасходов,
	0,
	ВыручкаИШтрафы.СуммаВыручки,
	ВыручкаИШтрафы.Партнер,
	ВыручкаИШтрафы.СуммаРасходы,
	0,
	0,
	0,
	0,
	0
ИЗ
	ВыручкаИШтрафы КАК ВыручкаИШтрафы
{ГДЕ
	ВыручкаИШтрафы.Серия.*,
	ВыручкаИШтрафы.Номенклатура.*}

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ОстаткиНаСкладе.Серия,
	ОстаткиНаСкладе.Номенклатура,
	"Приходный ордер",
	"",
	0,
	0,
	0,
	0,
	0,
	ОстаткиНаСкладе.ОстатокНаСкладе,
	0,
	0,
	0
ИЗ
	ОстаткиНаСкладе КАК ОстаткиНаСкладе
{ГДЕ
	ОстаткиНаСкладе.Серия.*,
	ОстаткиНаСкладе.Номенклатура.*}

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	РасчетФОТ.Серия,
	РасчетФОТ.Номенклатура,
	РасчетФОТ.Ссылка,
	РасчетФОТ.СтатьяРасходов,
	0,
	0,
	0,
	РасчетФОТ.СуммаРасхода,
	0,
	0,
	0,
	0,
	0
ИЗ
	РасчетФОТ КАК РасчетФОТ
{ГДЕ
	РасчетФОТ.Серия.*,
	РасчетФОТ.Номенклатура.*,
	РасчетФОТ.СтатьяРасходов}

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ДанныеПродажи.Серия,
	ДанныеПродажи.Номенклатура,
	ДанныеПродажи.Регистратор,
	0,
	ДанныеПродажи.КоличествоПродажа,
	ДанныеПродажи.СуммаВыручки,
	ДанныеПродажи.Партнер,
	0,
	0,
	0,
	0,
	0,
	0
ИЗ
	ДанныеПродажи КАК ДанныеПродажи
{ГДЕ
	ДанныеПродажи.Серия.*,
	ДанныеПродажи.Номенклатура.*}

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ТоварыНаСкладахОбороты.Серия,
	ТоварыНаСкладахОбороты.Номенклатура,
	"Ордер на перемещение товаров",
	0,
	0,
	0,
	0,
	0,
	0,
	0,
	СУММА(ВЫБОР
			КОГДА ТоварыНаСкладахОбороты.Регистратор ССЫЛКА Документ.ОрдерНаПеремещениеТоваров
					И ВЫРАЗИТЬ(ТоварыНаСкладахОбороты.Регистратор КАК Документ.ОрдерНаПеремещениеТоваров).ПомещениеПолучатель.ВЭД_ВидПомещения = &ВидПомещения
				ТОГДА -ТоварыНаСкладахОбороты.ВНаличииРасход
			КОГДА ТоварыНаСкладахОбороты.Регистратор ССЫЛКА Документ.РасходныйОрдерНаТовары
					И ВЫРАЗИТЬ(ТоварыНаСкладахОбороты.Регистратор КАК Документ.РасходныйОрдерНаТовары).Помещение.ВЭД_ВидПомещения = &ВидПомещенияБрак
				ТОГДА -ТоварыНаСкладахОбороты.ВНаличииРасход
			ИНАЧЕ ТоварыНаСкладахОбороты.ВНаличииПриход
		КОНЕЦ),
	0,
	0
ИЗ
	РегистрНакопления.ТоварыНаСкладах.Обороты(
			,
			,
			Регистратор,
			Склад В (&Склад)
				И Помещение.ВЭД_ВидПомещения = &ВидПомещенияБрак) КАК ТоварыНаСкладахОбороты
{ГДЕ
	ТоварыНаСкладахОбороты.Серия.*,
	ТоварыНаСкладахОбороты.Номенклатура.*}

СГРУППИРОВАТЬ ПО
	ТоварыНаСкладахОбороты.Серия,
	ТоварыНаСкладахОбороты.Номенклатура

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ТоварыНаСкладахОстатки.Серия,
	ТоварыНаСкладахОстатки.Номенклатура,
	"Ордер на перемещение товаров",
	0,
	0,
	0,
	0,
	0,
	0,
	0,
	0,
	ТоварыНаСкладахОстатки.ВНаличииОстаток,
	0
ИЗ
	РегистрНакопления.ТоварыНаСкладах.Остатки(
			,
			Склад В (&Склад)
				И Помещение.ВЭД_ВидПомещения = &ВидПомещенияПроизводство) КАК ТоварыНаСкладахОстатки
{ГДЕ
	ТоварыНаСкладахОстатки.Серия.*,
	ТоварыНаСкладахОстатки.Номенклатура.*}

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	СтоимостьМатериалов.Номенклатура,
	СтоимостьМатериалов.Регистратор,
	"",
	0,
	0,
	0,
	СтоимостьМатериалов.СуммаРасходы,
	СтоимостьМатериалов.Количество,
	0,
	0,
	0,
	0
ИЗ
	СтоимостьМатериалов КАК СтоимостьМатериалов
{ГДЕ
	СтоимостьМатериалов.Номенклатура.*}
	
